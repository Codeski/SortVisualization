import org.apache.tools.ant.filters.*

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'edu.sc.seis.macAppBundle'

group = 'com.codeski'
version = '0.5.0-SNAPSHOT'
sourceCompatibility = '1.7'

task filter(type: Copy) {
  from 'src/main/java'
  into 'build/sources'
  filter ReplaceTokens, tokens: [
    version: project.version
  ]
}

compileJava {
  source = 'build/sources'
}
compileJava.dependsOn filter

jar {
  manifest {
    attributes 'Implementation-Title': 'SortVisualization',
        'Implementation-Version': version,
        'Main-Class': 'com.codeski.sortvisualization.SortVisualization'
  }
}

buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.6.1'
    classpath 'gradle.plugin.edu.sc.seis:macAppBundle:2.1.5'
  }
}

launch4j {
  mainClassName = 'com.codeski.sortvisualization.SortVisualization'
  icon = '../../src/main/resources/icon.ico'
  jar = "lib/SortVisualization-" + project.version + ".jar"
}

macAppBundle {
  mainClassName = 'com.codeski.sortvisualization.SortVisualization'
  icon = 'src/main/resources/icon.icns'
  javaProperties.put("apple.laf.useScreenMenuBar", "true")
}
